apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

dependencies {
	api 'io.github.adempiere:withholding-engine:1.2.8'
	compileOnly fileTree(dir: 'dependences', include: ['*.jar'])
	compileOnly fileTree(dir: 'lib', include: ['*.jar'])
}

sourceSets {
    main {
         java {
            srcDirs = ['core/src/main/java/base', 'financial/src/main/java/base', 'withholding/src/main/java/base', 'human_resource/src/main/java/base']
         }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

def entityType = 'LVE'
version = 1//System.getenv("ADEMPIERE_LIBRARY_VERSION")

jar {
    manifest {
        attributes("Implementation-Title": "Venezuela Location for ADempiere",
                   "Implementation-Version": version, 
                   "EntityType": entityType)
    }
}

publishing {
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty("sonatypeUsername")
                password = findProperty("sonatypePassword")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
        	groupId 'io.github.adempiere'
            artifactId 'location-venezuela'
            version
           	from components.java
           	pom {
                name = 'ADempiere Venezuela Location'
                description = 'This library have all related to ADempiere Venezuela Location own Laws for Venezuela'
                url = 'http://erpya.com/'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/adempiere/LVE.git'
                    developerConnection = 'scm:git:ssh://github.com/adempiere/LVE.git'
                    url = 'http://github.com/adempiere/LVE'
                }
            }
		}
	}
}

signing {
	def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
